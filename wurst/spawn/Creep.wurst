package Creep
import Entity
import OrderIds
import StandardTextTags


public class Creep extends UnitEntity
	vec2 lastTarget = ZERO2
	var oldDist = 9999.
	var smart = false
	var bounty = 2

	construct(unit u, int bounty)
		super(u)
		this.bounty = bounty
		idleSeconds -= GetRandomInt(0, 4)
		EventListener.add(actor, EVENT_PLAYER_UNIT_DEATH) ->
			let killer = GetKillingUnit().getEntity()
			if killer.owner.isEnemyOf(owner)
				createGoldBountyTextTag(actor, bounty, killer.owner)
				killer.owner.addGold(bounty)



	override function update()
		super.update()

	override function slowUpdate()
		super.slowUpdate()
		if idleSeconds % 5 == 0
			if smart
				attack(lastTarget)
				smart = false
			else
				let dist = getPos().distanceTo2dSq(lastTarget)
				if dist > oldDist
					smart = true
					actor.issuePointOrderById(OrderIds.smart, lastTarget)
				oldDist = dist

	function attack(vec2 target)
		lastTarget = target
		oldDist = getPos().distanceTo2dSq(target)
		actor.issuePointOrderById(OrderIds.attack, target)
